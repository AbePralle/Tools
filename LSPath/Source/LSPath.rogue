#===============================================================================
# LSPath.rogue
# October 2, 2020 by Abe Pralle
#===============================================================================
$requireRogue "1.7.7"

$define VERSION "1.1"
$define DATE    "November 7, 2020 by Abe Pralle"

uses Utility/CommandLineParser

LSPath( System.command_line_arguments )

class LSPath
  PROPERTIES
    filepaths   = StringTable<<String>>()
    show_hidden = false
    grep        : String[]
    name        : String[]

  METHODS
    method init( args:String[] )
      local command = CommandLineParser().
      [
        option( "--grep=",  &alias="-g", &multi )
        option( "--help",   &aliases=["-h","-?"] )
        option( "--hidden", &alias="-a" )
        option( "--name=",  &alias="-n", &multi )
        on_unknown( this=>print_usage )
      ].parse( args )

      if (command//options//help)
        print_usage
        System.exit
      endIf

      show_hidden = command//options//hidden->Logical

      if (not command//args) command.ensure_list( "args" ).add( "**" )

      grep = command//options//grep.to_list<<String>>
      name = command//options//name.to_list<<String>>

      forEach (arg in command//args.to_list<<String>>)
        if (File.exists(arg))
          add( arg )
        else
          forEach (filepath in File.listing(arg,&ignore_hidden=not show_hidden))
            add( filepath )
          endForEach
        endIf
      endForEach

      println( forEach in filepaths )

    method add( filepath:String )
      if (not show_hidden and File.filename(filepath).begins_with('.')) return
      if (not filepath.contains(forEach in name)) return
      filepath = File.absolute_filepath( filepath )
      if (grep.count)
        if (File.is_folder(filepath)) return
        contingent
          local remaining_grep = grep.cloned
          forEach (line in LineReader(File(filepath)))
            forEach (pattern at index in remaining_grep step -1)
              if (line.contains_pattern(pattern))
                remaining_grep.remove_at( index )
                sufficient (remaining_grep.is_empty)
              endIf
            endForEach
          endForEach
          return # did not find all patterns
        endContingent
      endIf
      filepaths[ filepath ] = filepath

    method print_usage
      println "=" * 80
      println "LSPath v" + VERSION
      println DATE
      println "=" * 80
      println @|USAGE
               |  lspath <options> [filepath1 filepath2 ...]
               |
               |OPTIONS
               |  --grep=<pattern>, -g <pattern>
               |    Only prints filepaths of files that contain one or more given patterns.
               |    Despite the name these are "wildcard" patterns, not classic "grep" regular
               |    expressions. Patterns are applied line by line. Example:
               |      lspath --grep="A*Z"
               |    prints filepaths of all files containing a line that starts with A and ends
               |    with Z. Specifying multiple grep patterns requires that a file contain all
               |    patterns in order to have its filepath printed.
               |
               |  --help, -h, -?
               |    Print this help text.
               |
               |  --hidden, -a
               |    Show all hidden files.
               |
               |  --name=<name>, -n <name>
               |    Only print filepaths containing the given name. Multiple names may be
               |    specified in which case all names must be found to print a filepath.
               |
               |WILDCARD PATTERNS
               |  Put patterns in quotes to ensure that LSTree's non-standard wildcard patterns
               |  are correctly processed. By example:
               |    "*"            # All files in current folder
               |    "**"           # All files, recursively
               |    "**/*.rogue"   # All .rogue files, recursively
               |  Finally, '?' can be used to match any single character.
               |
               |EXAMPLES
               |  # Show filepaths for all .cpp, and .h files
               |  lspath "**/*.cpp" "**/*.h"
endClass

